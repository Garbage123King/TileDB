_realname=tiledb
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.9000
pkgrel=1
pkgdesc="Storage management library for sparse and dense array data (mingw-w64)"
arch=("any")
url="https://tiledb.com/"
license=("MIT")
depends=("${MINGW_PACKAGE_PREFIX}-lz4"
         "${MINGW_PACKAGE_PREFIX}-aws-sdk-cpp"
         "${MINGW_PACKAGE_PREFIX}-bzip2"
         "${MINGW_PACKAGE_PREFIX}-zlib"
         "${MINGW_PACKAGE_PREFIX}-zstd"
         "${MINGW_PACKAGE_PREFIX}-file"
         "${MINGW_PACKAGE_PREFIX}-dlfcn"
         )
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-curl"
             )
options=("staticlibs" "strip")
source_dir="$TILEDB_HOME"

build() {
  [[ -d ${source_dir}/build-${MINGW_CHOST} ]] && rm -rf ${source_dir}/build-${MINGW_CHOST}
  mkdir -p ${source_dir}/build-${MINGW_CHOST} && cd ${source_dir}/build-${MINGW_CHOST}

  if [ "$CARCH" == "i686" ]; then
  export CFLAGS="-mfpmath=sse -msse2"
  export CXXFLAGS="-mfpmath=sse -msse2"
  fi

  export MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX="

#    -DCMAKE_BUILD_TYPE=Release \
#    -DCMAKE_BUILD_TYPE=RelWithDebInfo \

  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DBUILD_SHARED_LIBS=OFF \
    -DTILEDB_S3=ON \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DTILEDB_STATIC=ON \
    -DCOMPILER_SUPPORTS_AVX2=OFF \
    -DTILEDB_SKIP_S3AWSSDK_DIR_LENGTH_CHECK=ON \
    -DTILEDB_WERROR=OFF \
    ..
  make VERBOSE=1
  make -C tiledb VERBOSE=1 -j ${NUMBER_OF_PROCESSORS}

  make -C tiledb/test tiledb_unit -j ${NUMBER_OF_PROCESSORS}

  make -C tiledb tests -j ${NUMBER_OF_PROCESSORS}
  make examples
}

package() {
  cd ${source_dir}/build-${MINGW_CHOST}
  make DESTDIR="${pkgdir}" -C tiledb install
}

check (){

  cd ${source_dir}/build-${MINGW_CHOST}

  export TILEDB_VFS_S3_REQUEST_TIMEOUT_MS=10000 #6000
  export TILEDB_VFS_S3_AWS_ACCESS_KEY_ID=minio
  export TILEDB_VFS_S3_AWS_SECRET_ACCESS_KEY=miniosecretkey
  export TILEDB_VFS_S3_SCHEME=http

  # ... start-process in install-minio.ps1 script fails
  # apparently due to pre-existing tmp/TMP/TEMP env vars
  ( unset tmp; unset TMP; unset TEMP; powershell ${TILEDB_HOME}/scripts/install-minio.ps1 )

  # always avoid the test that fails due to our 'non-standard' config options set above
  exclude_tests="~\"C API: Test config iter\""
  if [ "$CARCH" == "i686" ]; then
    # test failures occurring in 32bit environ, for now avoid
    echo "add unit tests skipped as too many currently fail with 32bit mingw!"

    exclude_tests="${exclude_tests} ~\"CPP API: Test consolidation with timestamps\\, global read\\, all cells same coords\\, with memory budget\""
    exclude_tests="${exclude_tests} ~\"CPP API: Test consolidation with timestamps\\, global read\\, same cells across tiles\\, with memory budget\""
    exclude_tests="${exclude_tests} ~\"Compression-RLE: Test compression parameter calculation of strings\""
    exclude_tests="${exclude_tests} ~\"C API: Test importing from a buffer\""
    exclude_tests="${exclude_tests} ~\"C\\ API:\\ Test\\ dense\\ array\\,\\ sorted\\ reads\""
    exclude_tests="${exclude_tests} ~\"C\\ API:\\ Test\\ dense\\ array\\,\\ encrypted\""

    prev_excluded=""
    prev_excluded="${prev_excluded} \"CPP API: Test consolidation with timestamps\\, global read\\, all cells same coords\\, with memory budget\""
    prev_excluded="${prev_excluded} \"CPP API: Test consolidation with timestamps\\, global read\\, same cells across tiles\\, with memory budget\""
    prev_excluded="${prev_excluded} \"Compression-RLE: Test compression parameter calculation of strings\""
    prev_excluded="${prev_excluded} \"C API: Test importing from a buffer\""

  else
    echo "no additional test exclusions"
  fi
  # S3 operations in particular cause problems, stick to native
  echo "run tiledb_unit with exclusions"
  ./tiledb/test/tiledb_unit.exe -d=yes --vfs=native "${exclude_tests}"

  if [ x"${prev_excluded}" != x ]; then
    #now run some of the excluded ones that seem to have failed just because of the handle leakage issues
    echo "run tiledb only with some previously excluded items"
    ./tiledb/test/tiledb_unit.exe -d=yes --vfs=native "${prev_excluded}"
  fi

  # add location of libtiledb.dll so examples can run
  export PATH=${PATH}:$(pwd)/tiledb/tiledb

  echo 'running any examples/c_api/* found'

  BaseDir="$(pwd)"
  TestAppDir="$(pwd)/tiledb/examples/c_api"
  TestAppDataDir="$(pwd)/tiledb/examples/c_api/test_app_data"
  for exampleexe in $(ls ${TestAppDir}/*_c.exe) ;
  do
    cd ${TestAppDir}
    rm -rf ${TestAppDataDir}
    mkdir ${TestAppDataDir}
    cd ${TestAppDataDir}
    echo $exampleexe; $exampleexe;
    status=$?
    if (($status != 0)); then
      echo "ERROR: $exampleexe exited with $status"
    fi
  done
  cd ${TestAppDir}
  rm -rf ${TestAppDataDir}

  echo 'running any examples/cpp_api/* found'
  cd ${BaseDir}
  TestAppDir="$(pwd)/tiledb/examples/cpp_api"
  TestAppDataDir="$(pwd)/tiledb/examples/cpp_api/test_app_data"
  for exampleexe in $(ls ${TestAppDir}/*_cpp.exe) ;
  do
    cd ${TestAppDir}
    rm -rf ${TestAppDataDir}
    mkdir ${TestAppDataDir}
    cd ${TestAppDataDir}
    echo $exampleexe; $exampleexe;
    status=$?
    if (($status != 0)); then
      echo "ERROR: $exampleexe exited with $status"
    fi
  done
  cd ${TestAppDir}
  rm -rf ${TestAppDataDir}
  cd ${BaseDir}

  echo "done running examples"

}
